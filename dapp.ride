{-# STDLIB_VERSION 5     #-}
{-# SCRIPT_TYPE ACCOUNT  #-}
{-# CONTENT_TYPE DAPP    #-}

### keys ###

#value data account
let main = "main_asset"
let l = "lp_asset_"
let aa1 = "pool__"
let sa = "STAKING_AMOUNT_"
let la = "LEASE_AMOUNT_"
let ix = "ID_"
let rateFee = "percentage_rate_fee"
let deposit_height = "Deposit_Height_Of_"

#decimals for assets
let D8 = 100000000
let D6 = 1000000
let D10 = 10000000000

#address is valid
func getAddressIfValid(address: String) = toString(valueOrErrorMessage(addressFromString(address), "Can't parse " + address + " as address"))

#assetId is valid
func getAssetIfValid(assetId: String) = toBase58String(valueOrErrorMessage(assetInfo(fromBase58String(assetId)).value().id, "Asset Id: " + assetId +  " is invalid."))

#get total amount block in smart contract for asset
func getAmountBlockAsset(assetId:String) = match getInteger(this, assetId){
  case i: Int => i
  case _ => 0
}

#get asset details - name
func getAssetName(assetId: ByteVector) = match assetInfo(assetId){
  case asset:Asset => asset.name
  case _ => throw("Can't find asset")
}

#config
let ac = "activate"
let poolAc = "activate_pools"
let swapAc = "activate_swap"
let liquidityAc = "activate_liquidity"
let bcD = "block_distribuition"
# let typeBcd = "type_block_distribution"
let total_locked = "Total_Tokens_Locked_"

#assets
let xtn = "xtn_asset"
let usdt = "usdt_asset"
let usdc = "usdc_asset"
let ngnn = "ngnn_asset"
let eurn = "eurn_asset"
let waves = "WAVES"
let aWaves = "accept_waves"
let WAVES = unit

#key for pools
func kp(asset1: String, asset2: String) = (aa1 + asset1 + "_" + asset2)

#returns if pool is valid
func validPoolK(key:String) = match getInteger(this, key){
  case i: Int => i
  case _ => false
}

#returns value that the account has in the pool
func kpAddress(address:String, key:String) = match (getInteger(this,key + "_" + address)){
  case t: Int => t
  case _ => 0
}

#returns value that the account has in the pool for asset block
func poolAA(address:String, key:String, asset: String) = match getInteger(this, address + "_" + key + "_" + asset){
  case a: Int => a
  case _ => 0
}

#return total asset for pool in smart contract
func assetP(key:String, asset: String) = match getInteger(this,key+"_"+asset){
  case a: Int => a
  case _ => 1
} 

#return height for pool address key
func pkey(Asset1: String,Asset2: String, address: String) = (deposit_height + kp(Asset1, Asset2) + "_" + address)

#return the value for asset lp blocked in stake
func lpBlocked(address: String, key: String) = match getInteger(this, "STAKE_LP_" +address + "_"+key){
  case a: Int => a
  case _ => 0
}

#admin verification
func adminVerification() = match (getString(this, "admin")) {
    case t: String => Address(fromBase58String(t))
    case _ => this
} 

#validate asset1 and asset2
func validateAsset(assetS: ByteVector|Unit, stringAsset: String,acWaves: Boolean) = if(assetS == WAVES && stringAsset == waves) then if(acWaves) then true else throw("The smart contract does not currently support Waves.") else if(assetS == fromBase58String(stringAsset)) then true else false

#return the total asset blocked in pool
func totalAssetPool(key:String, assetLp: Boolean) = match getInteger(this, key){
  case a: Int => a
  case _ => if(assetLp) then 1*D8 else 1
}

#return the asset id for pay in swap
func valid(assetPay: ByteVector|Unit, pair1: String, pair2: String) = {
  let pair1_58 = if(pair1 == waves) then WAVES else fromBase58String(pair1)
  let pair2_58 = if(pair2 == waves) then WAVES else fromBase58String(pair2)
  if(assetPay == pair1_58)
   then pair2_58
  else pair1_58
}

#### Constructor for start smart contract ####
@Callable(i)
# func constructor(MainAssetId: String, XtnAssetId: String, UsdtAssetId:String, UsdcAssetId: String, NgnnAssetId: String, acceptWaves: Boolean, percentageRateSwap: Int, blockDistribution: Int) = {
func constructor(MainAssetId: String, XtnAssetId: String, UsdtAssetId:String, UsdcAssetId: String, NgnnAssetId: String, EurnAssetId: String, percentageRateSwap: Int, blockDistribution: Int, acceptWaves: Boolean) = {
    let keyPoolUsdtWaves = kp(UsdtAssetId, waves)
    if ((i.caller == this || i.caller == adminVerification()))
      then if(valueOrElse(getInteger(this, keyPoolUsdtWaves), 0) == 0)
       then{
        #xtn - all assets
        let keyPoolXtnUsdt = kp(XtnAssetId, UsdtAssetId)
        let keyPoolXtnUsdc = kp(XtnAssetId, UsdcAssetId)
        let keyPoolXtnNgnn = kp(XtnAssetId, NgnnAssetId)
        let keyPoolXtnWaves = kp(XtnAssetId, waves)
        let keyPoolXtnMainAsset = kp(XtnAssetId, MainAssetId)
        let keyPoolXtnEurn = kp(XtnAssetId, EurnAssetId)

        #usdc - all assets
        let keyPoolUsdcUsdt = kp(UsdcAssetId, UsdtAssetId)
        let keyPoolUsdcNgnn = kp(UsdcAssetId, NgnnAssetId)
        let keyPoolUsdcWaves = kp(UsdcAssetId, waves)
        let keyPoolUsdcMainAsset = kp(UsdcAssetId, MainAssetId)
        let keyPoolUsdcEurn = kp(UsdcAssetId, EurnAssetId)

        #usdt - all assets
        let keyPoolUsdtNgnn = kp(UsdtAssetId, NgnnAssetId)
        let keyPoolUsdtMainAsset = kp(UsdtAssetId, MainAssetId)
        let keyPoolUsdtEurn = kp(UsdtAssetId, EurnAssetId)

        #ngnn - all assets
        let keyPoolNgnnWaves = kp(NgnnAssetId, waves)
        let keyPoolNgnnMainAsset = kp(NgnnAssetId, MainAssetId)
        let keyPoolNgnnEurn = kp(NgnnAssetId, EurnAssetId)

        #main asset - all assets
        let keyPoolMainWaves = kp(MainAssetId, waves)
        let keyPoolMainEurn = kp(MainAssetId, EurnAssetId)

        #eurn - all assets
        let keyPoolEurnWaves = kp(EurnAssetId, waves)
      [
      StringEntry(main, MainAssetId), 
      StringEntry(xtn, XtnAssetId), 
      StringEntry(usdc, UsdcAssetId), 
      StringEntry(usdt, UsdtAssetId), 
      StringEntry(ngnn, NgnnAssetId), 
      BooleanEntry(aWaves, acceptWaves), 
      IntegerEntry(rateFee, percentageRateSwap),
      BooleanEntry(ac, true), 
      BooleanEntry(swapAc, false), 
      BooleanEntry(poolAc, false), 
      BooleanEntry(liquidityAc, false), 
      IntegerEntry(bcD, blockDistribution),
      IntegerEntry(keyPoolXtnUsdt, 1),
      IntegerEntry(keyPoolXtnWaves, 1),
      IntegerEntry(keyPoolXtnUsdc, 1),
      IntegerEntry(keyPoolXtnNgnn, 1),
      IntegerEntry(keyPoolXtnMainAsset, 1),
      IntegerEntry(keyPoolUsdcUsdt,1),
      IntegerEntry(keyPoolUsdcNgnn,1),
      IntegerEntry(keyPoolUsdcWaves,1),
      IntegerEntry(keyPoolUsdtMainAsset,1),
      IntegerEntry(keyPoolUsdtNgnn,1),
      IntegerEntry(keyPoolUsdtWaves,1),
      IntegerEntry(keyPoolNgnnWaves,1),
      IntegerEntry(keyPoolNgnnMainAsset,1),
      IntegerEntry(keyPoolMainWaves,1),
      IntegerEntry(keyPoolEurnWaves, 1),
      IntegerEntry(keyPoolMainEurn,1),
      IntegerEntry(keyPoolNgnnEurn,1),
      IntegerEntry(keyPoolUsdcEurn,1),
      IntegerEntry(keyPoolXtnEurn,1),
      IntegerEntry(keyPoolUsdtEurn,1),
      IntegerEntry(keyPoolUsdcMainAsset,1),
      StringEntry(eurn,EurnAssetId)
      ]
      }
     else throw("Smart Contract has been started")
    else throw("Only the Admin itself can invoke this function")
}

#### Admin settings and options ####

#power on/off dApp
@Callable(i)
func activate(v: Boolean) = {
    if((i.caller == this || i.caller == adminVerification()))
      then [BooleanEntry(ac, v)]
    else throw("Only the Admin itself can invoke this function")
}

#activate and desactivate pools/swap/liquidity
@Callable(i)
func maintenance(pools: Boolean, swap: Boolean, stake: Boolean) = {
  if((i.caller == this || i.caller == adminVerification()))
   then [BooleanEntry(poolAc, pools), BooleanEntry(swapAc, swap), BooleanEntry(liquidityAc, stake)]
  else throw("Only the Admin itself can invoke this function")
}

#edit percentual rate for fees swap
@Callable(i)
func percentualRateFeeSwapEdit(rate: Int) = {
  if((i.caller == this || i.caller == adminVerification()))
   then [IntegerEntry(rateFee, rate)]
  else throw("Only the Admin itself can invoke this function")
}

# edit accept Waves
@Callable(i)
func acceptWaves(activate: Boolean) = {
  if((i.caller == this || i.caller == adminVerification()))
   then [BooleanEntry(aWaves, activate)]
  else throw("Only the Admin itself can invoke this function")
}

#edit block distribution
@Callable(i)
func blockDistribution(newValue: Int) = {
  if((i.caller == this || i.caller == adminVerification()))
   then [IntegerEntry(bcD, newValue)]
  else throw("Only the Admin itself can invoke this function")
}

#add admin for smart contract
@Callable(i)
func addAdmin(address: String) = {
  if((i.caller == this || i.caller == adminVerification()))
   then{
     [
      StringEntry("admin", address)
     ]
   }
  else throw("Only the Admin itself can invoke this function")
}

#create lp asset for initial config
@Callable(i)
func lpAssetCreateForPairPool(asset1: String, asset2: String, Name: String, customName: Boolean) = {
  let nameAsset1 = if(asset1 == waves) then waves else getAssetName(fromBase58String(asset1))
  let nameAsset2 = if(asset2 == waves) then waves else getAssetName(fromBase58String(asset2))
  let name = if(customName) then Name else nameAsset1 + "_" + nameAsset2
  let key = validPoolK(kp(asset1, asset2))
  if((i.caller == this || i.caller == adminVerification()))
   then if(key != false)
    then {
      let assetLpCreate = Issue(name, "LP asset for pool liquidity in Dolphin Finance pair " + asset1 + "_"+ asset2, 1*D8,8,true)
      let idAssetLp = calculateAssetId(assetLpCreate)
      [
       StringEntry(l+kp(asset1, asset2), toBase58String(idAssetLp)),
       assetLpCreate
      ]
    }
    else throw("Invalid Pool Key. Try inverting the assets")
  else throw("Only the Admin itself can invoke this function")
}


### add and remove stake ###

#stake main asset and lp assets valid in smart contract
@Callable(i)
func deposit(pool: Boolean, Asset1: String, Asset2: String) = {
  #main validations
  let power = getBooleanValue(this, ac)
  let liquidityPower = getBooleanValue(this, liquidityAc)
  # let acWaves = getBooleanValue(this, aWaves)

  #payment 
  # let XTN = fromBase58String(getStringValue(this, xtn))
  # let USDC = fromBase58String(getStringValue(this, usdc))
  # let USDT = fromBase58String(getStringValue(this, usdt))
  # let NGNN = fromBase58String(getStringValue(this, ngnn))
  let MainAsset = (getStringValue(this, main))
  let lpAssetId = if(pool) then (getStringValue(this,l+kp(Asset1, Asset2))) else MainAsset #get LP assetId configured for pool in smart contract by admin

  #validation and more
  if((power))
   then if(!(liquidityPower))
    then if((i.payments[0].assetId != fromBase58String(lpAssetId)))
     then throw("Invalid asset! Kindly Deposit Only " + MainAsset + " or valid asset lp")
     else{
      if(!(pool))
       then {
         let currentKey = toBase58String(i.caller.bytes)
         let currentAmount = match getInteger(this, currentKey) { 
          case a: Int => a 
          case _ => 0 
         }
         let h = if(currentAmount == 0) then height else valueOrElse(getIntegerValue(this,(deposit_height+ currentKey)), height)
         let newAmount = (currentAmount + i.payments[0].amount)
         let useramount = IntegerEntry(currentKey, newAmount)
         let userdepositheight = IntegerEntry((deposit_height+ currentKey), h)
         let previouslockbalance = match getInteger(this, "Total_Tokens_Locked:"){
          case a: Int => a
          case _ => 0 
         }
         let totaltoken = (previouslockbalance + i.payments[0].amount)
         let TLV = IntegerEntry("Total_Tokens_Locked:", totaltoken)
         [useramount, userdepositheight, TLV]
       }
       else{
         let poolKey = kp(Asset1, Asset2)
         let key = validPoolK(poolKey)
         if(key != false)
          then{
            let currentKey = toBase58String(i.caller.bytes)
            let currentAmount = match getInteger(this, "STAKE_LP_" +currentKey + "_"+poolKey) {
              case a: Int => a
              case _ => 0
            }
            let h = if(currentAmount == 0) then height else valueOrElse(getIntegerValue(this,(deposit_height+ "_"+ currentKey + "_"+poolKey)), height)
            let newAmount = (currentAmount + i.payments[0].amount)
            let useramount = IntegerEntry("STAKE_LP_" +currentKey + "_"+poolKey, newAmount)
            let userdepositheight = IntegerEntry((deposit_height+ "_"+ currentKey + "_"+poolKey), height)
            let previouslockbalance = match getInteger(this, "Total_Tokens_LP_Locked_" + poolKey){ 
              case a:Int => a
              case _ => 0
            }
            let totaltoken = (previouslockbalance + i.payments[0].amount)
            let TLV = IntegerEntry("Total_Tokens_LP_Locked_" + poolKey, totaltoken)
            [useramount, userdepositheight, TLV]
          }
          else throw("Invalid Pool key.")
       }
    }
   else throw("liquidity is currently under maintenance.")
  else throw("dApp is currently under maintenance.")
}

#remove stake for main asset and lp assets valid in smart contract
@Callable(i)
func remove(pool: Boolean, Asset1: String, Asset2: String,amount: Int) = {
  #main validations
  let power = getBooleanValue(this, ac)
  let liquidityPower = getBooleanValue(this, liquidityAc)
  # let acWaves = getBooleanValue(this, aWaves)

  #payment 
  let pmt =i.payments[0]
  # let XTN = fromBase58String(getStringValue(this, xtn))
  # let USDC = fromBase58String(getStringValue(this, usdc))
  # let USDT = fromBase58String(getStringValue(this, usdt))
  # let NGNN = fromBase58String(getStringValue(this, ngnn))
  let MainAsset = fromBase58String(getStringValue(this, main))

  #validation and more
  if((power))
   then if(!(liquidityPower)) 
    then{
    let currentKey = toBase58String(i.caller.bytes)
    let currentAmount = match (if(pool) then getIntegerValue(this,"STAKE_LP_" +currentKey + "_"+kp(Asset1, Asset2)) else getIntegerValue(this, currentKey)){
      case a: Int => a
      case _ => 0
    }
    let swapheightkeyname = if(pool) then (deposit_height+ "_"+ currentKey + "_"+kp(Asset1, Asset2)) else(deposit_height + currentKey)
    let previouslockbalance = if(pool) then getIntegerValue(this, "Total_Tokens_LP_Locked_" +kp(Asset1, Asset2)) else getIntegerValue(this, "Total_Tokens_Locked:")
    let totaltoken = (previouslockbalance - amount)
    let diff = (height - getIntegerValue(this, swapheightkeyname))
    let bcd = getIntegerValue(this, bcD)
    let reward = (diff * bcd) * D8
    let feeCalc = reward / (previouslockbalance/amount)
    if ((0 > amount))
      then throw(if(pool) then "Can't withdraw negative "+ Asset1 + "/" + Asset2 +  " amount" else "Can't withdraw negative Dolphin Token amount")
      else {
       let newAmount = (currentAmount - amount)
       if ((0 > newAmount))
        then throw(if(pool) then "Not enough Asset Lp " + Asset1 + "/" + Asset2 +  " balance" else "Not enough Dolphin Token balance")
          else[
            IntegerEntry(if(pool) then "STAKE_LP_" +currentKey + "_"+kp(Asset1, Asset2) else currentKey, newAmount), 
            IntegerEntry(if(pool) then "Total_Tokens_LP_Locked_" +kp(Asset1, Asset2) else "Total_Tokens_Locked:", totaltoken),
            ScriptTransfer(i.caller, amount, if(pool) then fromBase58String(getStringValue(this,l+kp(Asset1, Asset2))) else MainAsset), 
            ScriptTransfer(i.caller,feeCalc, MainAsset)
          ]
      }
    }
   else throw("liquidity is currently under maintenance.")
  else throw("dApp is currently under maintenance.")
}

#### pools - add and remove liquidity ####

#add liquidity
@Callable(i)
func addLiquidity(Asset1:String, Asset2: String) = {
  #main validations
  let power = getBooleanValue(this, ac) #checks if the smart contract is activate - ok
  let poolActivate = getBooleanValue(this, poolAc) #check if the pool in the smart contract is activate - ok
  let acWaves = getBooleanValue(this, aWaves) #check if the smart contract accept payment using Waves - ok
  let key = validPoolK(kp(Asset1, Asset2)) #get pool key (if existing) in smart contract for validation - ok

  #payment 
  let asset1 = i.payments[0].assetId #get asset1 used for payment  - ok
  let asset2 = i.payments[1].assetId #get asset2 used for payment - ok
  let XTN = fromBase58String(getStringValue(this, xtn)) #get XTN assetId configured in smart contract - ok
  let USDC = fromBase58String(getStringValue(this, usdc)) #get USDC assetId configured in smart contract - ok
  let USDT = fromBase58String(getStringValue(this, usdt)) #get USDT assetId configured in smart contract - ok
  let NGNN = fromBase58String(getStringValue(this, ngnn)) #get NGNN assetId configured in smart contract - ok
  let EURN = fromBase58String(getStringValue(this, eurn)) #get EURN assetId configured in smart contract - ok
  let MainAsset = fromBase58String(getStringValue(this, main)) #get Main assetId configured in smart contract - ok
  let lpAssetId = getStringValue(this,l+kp(Asset1, Asset2)) #get LP assetId configured for pool in smart contract by admin - ok

  let totalAsset1Pool = if(totalAssetPool(kp(Asset1, Asset2)+"_"+Asset1, false) < 2) then (i.payments[0].amount) else totalAssetPool(kp(Asset1, Asset2)+"_"+Asset1, false) 
  let totalAsset2Pool = if(totalAssetPool(kp(Asset1, Asset2)+"_"+Asset1, false) < 2) then (i.payments[1].amount) else totalAssetPool(kp(Asset1, Asset2)+"_"+Asset2,false)
  let totalLp = totalAssetPool(lpAssetId,true)
  let priceLpAsset = (totalAsset1Pool+totalAsset2Pool)/totalLp
  let quantityAsset2 = i.payments[0].amount*((totalAsset2Pool)/totalAsset1Pool)
  let rateTop = i.payments[1].amount + (i.payments[1].amount*2/100) #rate top 2%
  let rateBottom = i.payments[1].amount - (i.payments[1].amount*2/100) #rate top 2%
  let Validate = if(i.payments[1].amount == quantityAsset2 || i.payments[1].amount<rateTop || i.payments[1].amount>rateBottom) then true else false

  #validation and more
  if((power)) #if the smart contract is activate - ok
   then if(!(poolActivate)) #if pool is activate - ok
    then if(!(asset1 == asset2) && (validateAsset(asset1, Asset1,acWaves))&& (validateAsset(asset2,Asset2,acWaves))) #if asset1 and asset2 they're not the equal and asset payment and asset string is equal - ok
     then if((asset1 == XTN || asset1 == USDC || asset1 == USDT || asset1 == NGNN || asset1 == MainAsset || asset1 == WAVES || asset1 == EURN) && (asset2 == XTN || asset2 == USDC || asset2 == USDT || asset2 == NGNN || asset2 == MainAsset || asset2 == WAVES || asset2 == EURN)) #check if the asset1 and asset2 is valid with assets configured in the smart contract - ok
      then if(key != false) #if pool key is valid  - ok
       then if(Validate)
       then{
        #get old or new values for account
        let currentKey = toBase58String(i.caller.bytes) #get address for user - ok
        let currentAmount = kpAddress(currentKey,kp(Asset1, Asset2)) #get amount LP locked by address - ok
        let currentAmountAsset1 = poolAA(currentKey, kp(Asset1, Asset2), Asset1) #get amount locked by address for the asset1 if the lp token is not locked - ok
        let currentAmountAsset2 = poolAA(currentKey, kp(Asset1, Asset2), Asset2) #get amount locked by address for the asset2 if the lp token is not locked - ok
        let asset1pool = assetP(kp(Asset1, Asset2), Asset1) #get the total amount locked in smart contract for asset1 - ok
        let asset2pool = assetP(kp(Asset1, Asset2), Asset2) #get the total amount locked in smart contract for asset2 - ok
        let receiveLp = if(totalAsset1Pool == i.payments[0].amount) then 1*D8 else toInt(((toBigInt(i.payments[0].amount)*toBigInt(D8)+toBigInt(i.payments[1].amount)*toBigInt(D8))/((toBigInt(totalAsset1Pool)*toBigInt(D8)+toBigInt(totalAsset2Pool)*toBigInt(D8))/(toBigInt(totalLp))))) #get total asset lp which should be passed on based on the total pooled value of both assets - ok
        #new values based on old values
        let newcurrentAmountAsset1 = currentAmountAsset1 + i.payments[0].amount #updates the total of values ​​blocked by the address in the pool for asset1
        let newcurrentAmountAsset2 = currentAmountAsset2 + i.payments[1].amount #updates the total of values ​​blocked by the address in the pool for asset2
        let newcurrentAmount = currentAmount + receiveLp #updates the total of values ​​blocked by the address in the pool for lp asset
        let newasset1pool = asset1pool + i.payments[0].amount #updates the total of values ​​blocked in the pool for asset1
        let newasset2pool = asset2pool + i.payments[1].amount #updates the total of values ​​blocked in the pool for asset2
        [
         Reissue(fromBase58String(lpAssetId), receiveLp,true),
         ScriptTransfer(Address(i.caller.bytes),receiveLp,fromBase58String(lpAssetId)), #transfer lp asset for address - ok
         IntegerEntry(pkey(Asset1, Asset2, currentKey), height), #updates the lock height of values ​​in the pool - ok
         IntegerEntry(currentKey + "_" + kp(Asset1, Asset2) + "_" + Asset1, newcurrentAmountAsset1), #update the amount loked by address for the asset1 in pool
         IntegerEntry(currentKey + "_" + kp(Asset1, Asset2) + "_" + Asset2, newcurrentAmountAsset2), #update the amount loked by address for the asset2 in pool
         IntegerEntry(kp(Asset1, Asset2) + "_" + currentKey,newcurrentAmount), #updates the total of values ​​blocked by the address in the pool for lp asset
         IntegerEntry(kp(Asset1, Asset2)+"_"+Asset1, newasset1pool), #update the total amount of values blocked in pool for asset1
         IntegerEntry(kp(Asset1, Asset2)+"_"+Asset2, newasset2pool), #update the total amount of values blocked in pool for asset2
         IntegerEntry(Asset1, getAmountBlockAsset(Asset1) + i.payments[0].amount), #update the total amount of values blocked in smart contract for asset1
         IntegerEntry(Asset2, getAmountBlockAsset(Asset2) + i.payments[1].amount), #update the total amount of values blocked in smart contract for asset2
         IntegerEntry(lpAssetId, getAmountBlockAsset(lpAssetId) + receiveLp) #update the total amount of values blocked in pool for asset lp
        ]
       }
       else throw("Payment of invalid 1/1 amounts to the Pool. Try Asset2: {value:" + toString(i.payments[0].amount*((totalAsset2Pool)/totalAsset1Pool))  + "} Received: " + toString(i.payments[1].amount)+ " Validate: "+ toString(Validate))
      else throw("Invalid Pool Key.")
     else throw("Invalid Asset1 or Asset2!")
    else throw("Asset1 cannot be equal to Asset2")
   else throw("liquidity is currently under maintenance.")
  else throw("dApp is currently under maintenance.")
}

#remove liquidity
@Callable(i)
func removeLiquidity(Asset1: String, Asset2: String, Amount: Int) = {
  let currentKey = toBase58String(i.caller.bytes) #get address for user
  #main validations
  let power = getBooleanValue(this, ac) #checks if the smart contract is activate
  let poolActivate = getBooleanValue(this, poolAc) #check if the pool in the smart contract is activate
  let acWaves = getBooleanValue(this, aWaves) #check if the smart contract accept payment using Waves
  let poolKey = kp(Asset1, Asset2)
  let key = validPoolK(poolKey) #get pool key (if existing) in smart contract for validation

  let asset1 = if(Asset1 == waves) then if(acWaves) then WAVES else throw("The smart contract does not currently support Waves.") else fromBase58String(Asset1)
  let asset2 = if(Asset2 == waves) then if(acWaves) then WAVES else throw("The smart contract does not currently support Waves.") else fromBase58String(Asset2)
  let XTN = fromBase58String(getStringValue(this, xtn)) #get XTN assetId configured in smart contract
  let USDC = fromBase58String(getStringValue(this, usdc)) #get USDC assetId configured in smart contract
  let USDT = fromBase58String(getStringValue(this, usdt)) #get USDT assetId configured in smart contract
  let NGNN = fromBase58String(getStringValue(this, ngnn)) #get NGNN assetId configured in smart contract
  let MainAsset = fromBase58String(getStringValue(this, main)) #get Main assetId configured in smart contract
  let EURN = fromBase58String(getStringValue(this, eurn)) #get EURN assetId configured in smart contract
  let lpAssetId = getStringValue(this,l+kp(Asset1, Asset2)) #get LP assetId configured for pool in smart contract by admin
  let currentAmount = kpAddress(currentKey,kp(Asset1, Asset2)) #get amount LP locked by address
  let amountLpBlocked = lpBlocked(currentKey,poolKey)
  let totalLP = totalAssetPool(lpAssetId,true)
  let totalAsset1Pool =totalAssetPool(kp(Asset1, Asset2)+"_"+Asset1, false)
  let totalAsset2Pool = totalAssetPool(kp(Asset1, Asset2)+"_"+Asset2,false)

  #validation and more
  if((power)) #if the smart contract is activate
   then if(!(poolActivate)) #if pool is activate
    then if(!(Asset1 == Asset2)) #if asset1 and asset2 they're not the equal and asset payment and asset string is equal
     then if((asset1 == XTN || asset1 == USDC || asset1 == USDT || asset1 == NGNN || asset1 == MainAsset || asset1 == WAVES || asset1==EURN) && (asset2 == XTN || asset2 == USDC || asset2 == USDT || asset2 == NGNN || asset2 == MainAsset|| asset2 == WAVES || asset2==EURN)) #check if the asset1 and asset2 is valid with assets configured in the smart contract
      then if(key != false) #if pool key is valid 
       then if(!(currentAmount == 0))
        then if((currentAmount-amountLpBlocked) >= Amount)
         then if(totalAsset1Pool > 2 || totalAsset2Pool > 2)
          then{ 
            let receiveAsset1 = toInt(fraction(toBigInt(i.payments[0].amount),toBigInt(D8),toBigInt(totalLP))*toBigInt(totalAsset1Pool)/toBigInt(D8)) -1
            let receiveAsset2 = toInt(fraction(toBigInt(i.payments[0].amount),toBigInt(D8),toBigInt(totalLP))*toBigInt(totalAsset2Pool)/toBigInt(D8)) -1
            let asset1pool = assetP(kp(Asset1, Asset2), Asset1) #get the total amount locked in smart contract for asset1
            let asset2pool = assetP(kp(Asset1, Asset2), Asset2) #get the total amount locked in smart contract for asset2
            let currentAmountAsset1 = poolAA(currentKey, kp(Asset1, Asset2), Asset1) #get amount locked by address for the asset1 if the lp token is not locked
            let currentAmountAsset2 = poolAA(currentKey, kp(Asset1, Asset2), Asset2) #get amount locked by address for the asset2 if the lp token is not locked
            let newcurrentAmountAsset1 = currentAmountAsset1 - receiveAsset1 #updates the total of values ​​blocked by the address in the pool for asset1
            let newcurrentAmountAsset2 = currentAmountAsset2 - receiveAsset2 #updates the total of values ​​blocked by the address in the pool for asset2
            let newcurrentAmount = currentAmount - Amount #updates the total of values ​​blocked by the address in the pool for lp asset
            let newasset1pool = asset1pool - receiveAsset1 #updates the total of values ​​blocked in the pool for asset1
            let newasset2pool = asset2pool - receiveAsset2 #updates the total of values ​​blocked in the pool for asset2
            if(i.payments[0].assetId == fromBase58String(lpAssetId))
             then if(Amount == i.payments[0].amount)
              then
            [
             Burn(fromBase58String(lpAssetId), Amount),
             ScriptTransfer(Address(i.caller.bytes),receiveAsset1,asset1),
             ScriptTransfer(Address(i.caller.bytes),receiveAsset2,asset2),
             IntegerEntry(pkey(Asset1, Asset2, currentKey), height), #updates the lock height of values ​​in the pool
             IntegerEntry(currentKey + "_" + kp(Asset1, Asset2) + "_" + Asset1, newcurrentAmountAsset1), #update the amount loked by address for the asset1 in pool
             IntegerEntry(currentKey + "_" + kp(Asset1, Asset2) + "_" + Asset2, newcurrentAmountAsset2), #update the amount loked by address for the asset2 in pool
             IntegerEntry(kp(Asset1, Asset2) + "_" + currentKey,newcurrentAmount), #updates the total of values ​​blocked by the address in the pool for lp asset
             IntegerEntry(kp(Asset1, Asset2)+"_"+Asset1, newasset1pool), #update the total amount of values blocked in pool for asset1
             IntegerEntry(kp(Asset1, Asset2)+"_"+Asset2, newasset2pool), #update the total amount of values blocked in pool for asset2
             IntegerEntry(Asset1, getAmountBlockAsset(Asset1) - receiveAsset1), #update the total amount of values blocked in smart contract for asset1
             IntegerEntry(Asset2, getAmountBlockAsset(Asset2) - receiveAsset2), #update the total amount of values blocked in smart contract for asset2
             IntegerEntry(lpAssetId, getAmountBlockAsset(lpAssetId) - Amount) #update the total amount of values blocked in pool for asset lp
            ]
             else throw("Withdrawal amount lower than asset lp amount. Your Lp amount available: " + toString(Amount))
            else throw("Requires an lp asset as payment to remove locked amounts.")
          }
         else throw("Pool has no liquidity.")
        else throw("Amount to be withdrawn greater than the amount available for withdrawal. Available: " + toString(Amount))
       else throw("You have no locked values ​​in the pool")
      else throw("Invalid Pool Key.")
     else throw("Invalid Asset1 or Asset2!")
    else throw("Asset1 cannot be equal to Asset2")
   else throw("liquidity is currently under maintenance.")
  else throw("dApp is currently under maintenance.")
}

### swap for pairs valid ###

#swap function
@Callable(i)
func swap(Asset1: String, Asset2: String) = {
  let currentKey = toBase58String(i.caller.bytes) #get address for user
  #main validations
  let acWaves = getBooleanValue(this, aWaves) #check if the smart contract accept payment using Waves
  let power = getBooleanValue(this, ac) #checks if the smart contract is activate
  let swapActivate = getBooleanValue(this, swapAc) #check if the swap is activate
  let poolKey = kp(Asset1, Asset2)
  let key = validPoolK(poolKey) #get pool key (if existing) in smart contract for validation
  
  #payment 
  let asset1 = i.payments[0].assetId #get asset1 used for payment 
  let XTN = fromBase58String(getStringValue(this, xtn)) #get XTN assetId configured in smart contract
  let USDC = fromBase58String(getStringValue(this, usdc)) #get USDC assetId configured in smart contract
  let USDT = fromBase58String(getStringValue(this, usdt)) #get USDT assetId configured in smart contract
  let NGNN = fromBase58String(getStringValue(this, ngnn)) #get NGNN assetId configured in smart contract
  let MainAsset = fromBase58String(getStringValue(this, main)) #get Main assetId configured in smart contract
  let EURN = fromBase58String(getStringValue(this, eurn)) #get EURN assetId configured in smart contract
  let lpAssetId = getStringValue(this,l+kp(Asset1, Asset2)) #get LP assetId configured for pool in smart contract by admin


  #value for pool
  let totalAsset1 = totalAssetPool(kp(Asset1, Asset2)+"_"+Asset1,false)
  let totalAsset2 = totalAssetPool(kp(Asset1, Asset2)+"_"+Asset2,false)
  let feeRatePercentual = getIntegerValue(this, rateFee)

  let prAsset1 = (toBigInt(i.payments[0].amount)*fraction(toBigInt(D8), toBigInt(totalAsset2), toBigInt(totalAsset1)))
  let priceAsset1 = toInt((prAsset1 - (prAsset1/toBigInt(feeRatePercentual))*toBigInt(100))/toBigInt(D8))
  let prAsset2 = (toBigInt(i.payments[0].amount)*fraction(toBigInt(D8), toBigInt(totalAsset1), toBigInt(totalAsset2)))
  let priceAsset2 = toInt((prAsset2 - (prAsset2/toBigInt(feeRatePercentual))*toBigInt(100))/toBigInt(D8))
  let sendValue = if(asset1 == WAVES && Asset1 == waves) then priceAsset1 else {if(asset1 == fromBase58String(Asset1)) then priceAsset1 else priceAsset2}
  let assetUpdatePay = if(asset1 == WAVES) then waves else {if(asset1 == fromBase58String(Asset1)) then Asset1 else Asset2}
  let assetUpdateSwap = if(asset1 == WAVES) then waves else {if(asset1 == fromBase58String(Asset1)) then Asset2 else Asset1}
  let assetSendSwap = valid(i.payments[0].assetId,Asset1,Asset2)
  let n1s = assetP(kp(Asset1, Asset2), assetUpdateSwap)-sendValue
  let nw = getAmountBlockAsset(assetUpdateSwap) - sendValue
  let newUpda = if(0 > n1s) then 0 else n1s
  let newUpdateSwap = if(0 > nw) then 0 else nw
  let D = match i.payments[0].assetId{
    case o: Unit => pow(10,0,8,0,0,HALFUP)
    case x: ByteVector => pow(10,0,assetInfo(x).value().decimals,0,0,HALFUP)
  } 

  if(power)
   then if(!(swapActivate))
    then if(!(Asset1 == Asset2) && (validateAsset(asset1,Asset1,acWaves) || validateAsset(asset1,Asset2,acWaves))) #if asset1 and asset2 they're not the equal and asset payment and asset string is equal
     then if((asset1 == XTN || asset1 == USDC || asset1 == USDT || asset1 == NGNN || asset1 == MainAsset || asset1 == EURN || asset1 == WAVES)) #check if the asset1 and asset2 is valid with assets configured in the smart contract
      then if(key != false) #if pool key is valid
       then if(i.payments[1].assetId == MainAsset)
         then if(i.payments[1].amount == 300000)
          then if(totalAsset1 > 2 || totalAsset2 > 2)
           then{
            [
               ScriptTransfer(Address(i.caller.bytes), if(0>sendValue) then D else sendValue, assetSendSwap),
               IntegerEntry(assetUpdateSwap, newUpdateSwap),
               IntegerEntry(kp(Asset1, Asset2)+"_"+assetUpdateSwap, newUpda),
               IntegerEntry(kp(Asset1, Asset2)+"_"+assetUpdatePay,assetP(kp(Asset1, Asset2), assetUpdatePay)+(i.payments[0].amount)),
               IntegerEntry(assetUpdatePay, getAmountBlockAsset(assetUpdatePay)+(i.payments[0].amount)),
               IntegerEntry(toBase58String(MainAsset), getAmountBlockAsset(toBase58String(MainAsset)) + i.payments[1].amount)
            ]
          }
         else throw("Pool has no liquidity.")
        else throw("Swap denied! 0.003 Dolphin Token is required for the swap fee.")
       else throw("Invalid rate asset")
      else throw("Invalid Swap Key.")
     else throw("Invalid Asset1 or Asset2!")
    else throw("Asset1 cannot be equal to Asset2")
   else throw("Swap is currently under maintenance.")
  else throw("dApp is currently under maintenance.")
}

#validate transaction in smart contract
@Verifier(tx)
func verify() = match (tx) {
    case order: Order => true
    case e: ExchangeTransaction => true
    case tt: TransferTransaction => tt.recipient == Address(fromBase58String(getStringValue(this, "admin")))
    case _ => 
        sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
}
